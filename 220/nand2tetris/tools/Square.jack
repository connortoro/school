class Square {

    //INSTANCE VARIABLES
    field int x, y, size;

    //CONSTRUCTOR
    constructor Square new(int aX, int aY, int aSize) {
        let x = aX;
        let y = aY;
        let size = aSize;
        do draw();
        return this;
    }

    //DISPOSE
    method void dispose() {
        do Memory.deAlloc(this);
        return;
    }

    //DRAW
    method void draw() {
        do Screen.setColor(true);
        do Screen.drawRectangle(x, y, x + size, y + size);
        return;
    }

    //ERASE
    method void erase() {
        do Screen.setColor(false);
        do Screen.drawRectangle(x, y, x + size, y + size);
        return;

    }

    //INCREASE SIZE
    method void increaseSize() {
        do erase();
        let size = size + 1;
        do draw();
        return;
    }

    //DECREASE SIZE
    method void decreaseSize() {
        if(size > 1) {
            do erase();
            let size = size - 1;
            do draw();
        }
        return;
    }

    //MOVE UP
    method void moveUp() {
        if (y > 1) {
            do Screen.setColor(false);
            do Screen.drawRectangle(x, (y + size) - 1, x + size, y + size);
            let y = y - 2;
            do Screen.setColor(true);
            do Screen.drawRectangle(x, y, x + size, y + 1);
        }
        return;
    }

    //MOVE DOWN
    method void moveDown() {
        if ((y + size) < 254) {
            do Screen.setColor(false);
            do Screen.drawRectangle(x, y, x + size, y + 1);
            let y = y + 2;
            do Screen.setColor(true);
            do Screen.drawRectangle(x, (y + size) - 1, x + size, y + size);
        }
        return;
    }

    //MOVE LEFT
    method void moveLeft() {
        if (x > 1) {
            do Screen.setColor(false);
            do Screen.drawRectangle((x + size) - 1, y, x + size, y + size);
            let x = x - 2;
            do Screen.setColor(true);
            do Screen.drawRectangle(x, y, x + 1, y + size);
        }
        return;
    }

    //MOVE RIGHT
    method void moveRight() {
        if ((x + size) < 510) {
            do Screen.setColor(false);
            do Screen.drawRectangle(x, y, x + 1, y + size);
            let x = x + 2;
            do Screen.setColor(true);
            do Screen.drawRectangle((x + size) - 1, y, x + size, y + size);
        }
        return;
    }
}